apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.dragons.aurora'
version = '2.4'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

repositories {
    jcenter()
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'build/generated/source/proto/main/javalite'
        }
    }
}

jar {
    exclude("*.proto")
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.2'
    }

    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}
